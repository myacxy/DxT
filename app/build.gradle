buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.23.0'
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0

android {
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId 'net.myacxy.squinch'
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            useProguard true
            minifyEnabled true
            proguardFiles file('../proguard-files').listFiles().toList().toArray()
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            useProguard true
            minifyEnabled true
            proguardFiles file('../proguard-files').listFiles().toList().toArray()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '#dev'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

def Properties props = new Properties()
def propFile = new File(project.rootDir, 'signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('storeFile') && props.containsKey('storePassword') &&
            props.containsKey('keyAlias') && props.containsKey('keyPassword')) {
        android.signingConfigs.release.storeFile new File(project.rootDir, props['storeFile'])
        android.signingConfigs.release.storePassword props['storePassword']
        android.signingConfigs.release.keyAlias props['keyAlias']
        android.signingConfigs.release.keyPassword props['keyPassword']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    compile files('libs/retrotwitch-rxjava.jar')

    compile deps.support.appCompat
    compile deps.support.v4
    compile deps.support.design
    compile deps.support.recyclerView
    compile deps.support.palette
    compile deps.support.constraintLayout

    compile deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler
    compile(deps.rx.android) {
        exclude module: 'rxjava'
    }
    compile deps.kotlin.stdlib
    kapt deps.databinding

    compile deps.dashclock
    compile deps.fresco
    compile(deps.crashlytics) {
        transitive = true
    }
    compile deps.eventBus
    compile deps.logger

    debugCompile deps.leakCanary.real
    releaseCompile deps.leakCanary.noOp

    testCompile deps.junit
}

apply plugin: 'kotlin-android-extensions'
