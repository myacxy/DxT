buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.22.0'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url = "https://jitpack.io" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
}

project.ext {
    buildCode = 6
    buildName = "0.2.0"
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode = project.ext.buildCode
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            useProguard true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            useProguard false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-dev.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        prod {
            applicationId "net.myacxy.squinch"
            versionName = project.ext.buildName
        }

        dev {
            applicationId "net.myacxy.squinch.dev"
            versionName = project.ext.buildName + "#dev"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('storeFile') && props.containsKey('storePassword') &&
            props.containsKey('keyAlias') && props.containsKey('keyPassword')) {
        android.signingConfigs.release.storeFile file(props['storeFile'])
        android.signingConfigs.release.storePassword props['storePassword']
        android.signingConfigs.release.keyAlias props['keyAlias']
        android.signingConfigs.release.keyPassword props['keyPassword']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {

    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }

    compile files('libs/retrotwitch-rxjava.jar')

    Map<String, String> dependencies = rootProject.ext.dependencies;

    compile dependencies.dashclock
    compile dependencies.eventBus
    compile dependencies.logger
    compile(dependencies.rxAndroid) {
        exclude module: 'rxjava'
    }
    compile dependencies.butterknife
    annotationProcessor dependencies.butterknifeApt

    compile dependencies.appCompat
    compile dependencies.design
    compile dependencies.recyclerView
    compile dependencies.palette

//    compile dependencies.rxBindingRecyclerView
    compile 'com.facebook.fresco:fresco:0.14.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}