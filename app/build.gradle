buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.22.0'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url = "https://jitpack.io" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
}

project.ext {
    buildCode = 6
    buildName = "0.2.0"
    signingConfig = "..\\..\\..\\.signing\\squinch.properties"
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode = project.ext.buildCode
    }

    signingConfigs {
        release {
            if (project.hasProperty("signingConfig")
                    && new File(project.property("signingConfig")).exists()) {

                Properties props = new Properties()
                props.load(new FileInputStream(file(project.property("signingConfig"))))

                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            useProguard true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dev.pro'
        }
        debug {
            useProguard true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dev.pro'
        }
    }

    productFlavors {
        prod {
            applicationId "net.myacxy.squinch"
            versionName = project.ext.buildName
            signingConfig signingConfigs.release
        }

        dev {
            applicationId "net.myacxy.squinch.dev"
            versionName = project.ext.buildName + "#dev"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    compile files('libs/retrotwitch-rxjava.jar')

    Map<String, String> dependencies = rootProject.ext.dependencies;

    compile dependencies.dashclock
    compile dependencies.eventBus
    compile dependencies.logger
    compile(dependencies.rxAndroid) {
        exclude module: 'rxjava'
    }
    compile dependencies.butterknife
    annotationProcessor dependencies.butterknifeApt

    compile dependencies.appCompat
    compile dependencies.design
    compile dependencies.recyclerView
    compile dependencies.palette

    compile dependencies.materialEditText
//    compile dependencies.rxBindingRecyclerView
    compile 'com.facebook.fresco:fresco:0.14.1'
}